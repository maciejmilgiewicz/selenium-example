plugins {
    id 'java'
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

ext {
    junitVersion = '5.8.1'
    cucumberVersion = '7.14.0'
}

group 'com.macmil'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task uiTest {
    description = 'Runs UI tests.'
    group = 'verification'

    dependsOn assemble, testClasses

    doLast {
        javaexec {
            mainClass = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            def tags = findProperty('tags') ?: ''
            args = ['--plugin', 'pretty',
                    '--plugin', 'html:target/cucumber-report.html',
                    '--plugin', 'json:target/test-report.json',
                    '--glue', 'com.macmil', 'src/test/resources/features',
                    '--tags', tags]
        }
    }
}

dependencies {
    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.junit.vintage:junit-vintage-engine:${junitVersion}"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junitVersion}"
}

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

test {
    useJUnitPlatform()
}
